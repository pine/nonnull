apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    dryRun = false
    publish = true

    pkg {
        userOrg = rootProject.ext.userOrg
        repo = 'maven'
        name = project.name
        desc = project.description
        licenses = ['Unlicense']
        websiteUrl = rootProject.ext.websiteUrl
        issueTrackerUrl = rootProject.ext.issueTrackerUrl
        vcsUrl = rootProject.ext.vcsUrl
        publicDownloadNumbers = true
        githubRepo = rootProject.ext.githubRepo
        githubReleaseNotesFile = 'README.md'

        version {
            name = project.version
            vcsTag = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                name project.name
                url rootProject.ext.websiteUrl
                licenses {
                    license {
                        name 'Unlicense'
                        url 'https://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id 'pinemz'
                        name 'Pine Mizune'
                        email 'pinemz@gmail.com'
                    }
                }
                scm {
                    connection rootProject.ext.vcsUrl
                    developerConnection rootProject.ext.vcsUrl
                    url rootProject.ext.websiteUrl
                }
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
